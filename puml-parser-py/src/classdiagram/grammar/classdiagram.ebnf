start: "@startuml" (title | skinparam | package | class | enum | interface | relationship | note)* "@enduml"

title: "title" description

skinparam: "skinparam" sparam svalue

package: "package" package_name "{" (class | interface | enum | relationship | note)* "}"

class: "class" class_name stereotype? ("{" (attribute | method)* "}" | ":" (attribute | method)?)?

enum: "enum" enum_name "{" enum_value+ "}"

interface: "interface" interface_name "{" method* "}"

relationship: relation_source relation_type relation_target (":" description)?

note: "note" note_position? "of"? note_target note_content "end" "note"

note_position: "left"  -> left
             | "right"  -> right
             | "top"  -> top
             | "bottom" -> bottom

note_content: (NOTE_LINE)+

stereotype: "<<" stereotype_name ">>"

// Variables
variable: var ":" type

attribute: att_scope? visibility? variable
         | visibility? att_scope? variable

param_list: [variable ("," variable)*]

function: method_name "("param_list")" ":" type

method: att_scope? visibility? function
      | visibility? att_scope? function

// Attribute Scope
att_scope: "{static}" -> static
         | "{abstract}" -> abstract

relation_type: "--|>" -> inheritance
             | "--" -> association
             | "*--" -> composition
             | "o--" -> aggregation
             | "..>" -> dotted_dependency
             | "-->" -> dependency
             | "..|>" -> realization

// Variables and Methods visibility
visibility: "+" -> public
          | "-" -> private
          | "#" -> protected
          | "~" -> package

description: /.+/
hex_color: /#[0-9A-Fa-f][0-9A-Fa-f][0-9A-Fa-f][0-9A-Fa-f][0-9A-Fa-f][0-9A-Fa-f]/
COLOR: /#[a-zA-Z0-9]+/
relation_source: CNAME
relation_target: CNAME
note_target:CNAME 
type: /.+/
var: CNAME
sparam: CNAME
svalue: COLOR | CNAME | ESCAPED_STRING | SIGNED_NUMBER | (DIGIT)+ | 
attribute_name: CNAME
method_name: CNAME
class_name: CNAME
interface_name: CNAME
enum_value: CNAME
enum_name: CNAME
package_name: ESCAPED_STRING
stereotype_name: CNAME
relation_text: (WORD|ESCAPED_STRING|DIGIT|">"|"<")*
COMMENT: "'" /[^\n]*/
NOTE_LINE: /-.+/

%import common.CNAME
%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.WORD
%import common.DIGIT
%import common.WS
%ignore WS
%ignore COMMENT
%ignore /[\t]+/  // Ignore spaces and tabs